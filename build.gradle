plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.0'
}

String name = "InsightsWorldEditExtension"
group = 'dev.frankheijden.insights.extensions.worldedit'
version = '3.2.0'

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_16

subprojects {
    apply plugin: 'java'
    apply plugin: 'com.github.johnrengelman.shadow'

    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_16

    ext {
        baseName = name
    }

    repositories {
        mavenCentral()
        maven { url 'https://papermc.io/repo/repository/maven-public/' }
        maven { url 'https://maven.enginehub.org/repo/' }
        maven { url 'https://repo.fvdh.dev/releases' }
    }

    dependencies {
        compileOnly 'dev.frankheijden.insights:Insights:6.8.1'
        compileOnly 'io.papermc.paper:paper-api:1.17.1-R0.1-SNAPSHOT'
    }
}

dependencies {
    implementation project(path: ':Plugin', configuration: 'shadow')
    implementation project(path: ':WE', configuration: 'shadow')
    implementation project(path: ':FAWE', configuration: 'shadow')
}

shadowJar {
    archiveFileName = "${name}-${archiveVersion.orNull}.${archiveExtension.orNull}"
}

def outputTasks() {
    ["shadowJar"].stream().map({ tasks.findByPath(it) })
}

task copyJars(type: Copy) {
    outputTasks().forEach({ from(it) })
    rename '(.*)-all.jar', '$1.jar'
    into file('jars')
}

task cleanJars() {
    delete file('jars')
}

clean.dependsOn cleanJars
build.dependsOn shadowJar
build.dependsOn copyJars
